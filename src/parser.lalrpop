use lexer;
use ast;

grammar<'input>;

Semi<T>: Vec<T> = {
    <v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Program: ast::Program<'input> = {
    <stmts: Semi<Statement>> => ast::Program { stmts },
};

Statement: ast::Stmt<'input> = {
    Identifier Operator Expression => ast::Stmt::Statement(<>),
};

Identifier: ast::Identifier<'input> = {
    "identifier" => ast::identifier(<>),
};

Operator: ast::Operator = {
    "=" => ast::Operator::Assign,
    "+" => ast::Operator::Plus,
};

Expression: ast::Expression = {
    <left: Number> <op: Operator> <right: Number> =>
        ast::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
    <num: Number> => ast::Expression::Literal {
        value: num,
    },
};

Number: ast::Number = {
    "integer" => ast::number(<>),
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok<'input> {
        "identifier" => lexer::Tok::Identifier { name: <&'input str> },
        "=" => lexer::Tok::Assign,
        "+" => lexer::Tok::Plus,
        "integer" => lexer::Tok::Integer { value: <u32> },
        ";" => lexer::Tok::SemiColon,
    }
}

// vim: ft=rust
