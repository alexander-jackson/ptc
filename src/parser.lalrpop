use std::iter::FromIterator;

use lexer;
use ast;

grammar;

pub Program: ast::Program = {
    <lines:Line*> => ast::Program {
        statements: Vec::from_iter(lines.into_iter().flatten())
    },
};

Line: ast::Suite = {
    Statement,
    "\n" => vec![],
};

Statement: ast::Suite = {
    StatementList,
    <comp: CompoundStatement> => vec![comp],
};

StatementList: ast::Suite = {
    <s1: SmallStatement> <s2: (";" SmallStatement)*> ";"? "\n" => {
        let mut statements = vec![s1];
        statements.extend(s2.into_iter().map(|e| e.1));
        statements
    }
};

SmallStatement: ast::Statement = {
    AssignStatement,
    AugmentedAssignStatement,
    ExpressionStatement,
    PassStatement,
};

CompoundStatement: ast::Statement = {
    IfStatement,
    WhileStatement,
    FunctionDeclStatement,
};

AssignStatement: ast::Statement = {
    <ident: Identifier> "=" <expr: Expression> =>
        ast::Statement::Assign { ident: ident, expr: expr, },
};

AugmentedAssignOperator: ast::Operator = {
    "+=" => ast::Operator::PlusEquals,
    "-=" => ast::Operator::MinusEquals,
    "*=" => ast::Operator::MultiplyEquals,
    "/=" => ast::Operator::DivideEquals,
};

AugmentedAssignStatement: ast::Statement = {
    <ident: Identifier> <op: AugmentedAssignOperator> <expr: Expression> =>
        ast::Statement::AugmentedAssign { ident: ident, op: op, expr: expr, },
};

ExpressionStatement: ast::Statement = {
    <expr: Expression> =>
        ast::Statement::Expression { expr: expr, },
};

PassStatement: ast::Statement = {
    "pass" => ast::Statement::Pass,
};

IfStatement: ast::Statement = {
    "if" <expr: Expression> ":" "\n" "indent" <stmt: Statement> "unindent" =>
        ast::Statement::IfStatement {
            expr: expr,
            stmt: stmt,
        },
};

WhileStatement: ast::Statement = {
    "while" <expr: Expression> ":" "\n" "indent" <stmt: Statement> "unindent" =>
        ast::Statement::WhileStatement {
            expr: expr,
            stmt: stmt,
        },
};

ArgumentList: Vec<ast::Identifier> = {
    <l: (<Identifier> ",")*> <e: Identifier?> => match e {
        None => l,
        Some(e) => {
            let mut l = l;
            l.push(e);
            l
        },
    },
};

FunctionDeclStatement: ast::Statement = {
    "def" <ident: Identifier> "(" <args: ArgumentList> ")" ":" "\n" "indent" <stmt: Statement> "unindent" =>
        ast::Statement::FunctionDecl {
            name: ident,
            args: args,
            body: stmt,
        },
};

Operator: ast::Operator = {
    "+" => ast::Operator::Plus,
    "-" => ast::Operator::Minus,
    "*" => ast::Operator::Multiply,
    "/" => ast::Operator::Divide,
};

Expression: ast::Expression = {
    ConditionalExpression,
};

ConditionalExpression: ast::Expression = {
    OrTest,
};

OrTest: ast::Expression = {
    AndTest,
    <left: OrTest> "or" <right: AndTest> =>
        ast::Expression::BinaryOperation {
            left: Box::new(left),
            op: ast::Operator::LogicalOr,
            right: Box::new(right)
        },
};

AndTest: ast::Expression = {
    NotTest,
    <left: AndTest> "and" <right: NotTest> =>
        ast::Expression::BinaryOperation {
            left: Box::new(left),
            op: ast::Operator::LogicalAnd,
            right: Box::new(right)
        },
};

NotTest: ast::Expression = {
    Comparison,
    "not" <expr: NotTest> =>
        ast::Expression::UnaryOperation {
            op: ast::Operator::LogicalNot,
            expr: Box::new(expr),
        },
};

ComparisonOperator: ast::Operator = {
    "<" => ast::Operator::Less,
    ">" => ast::Operator::Greater,
    "<=" => ast::Operator::LessOrEqual,
    ">=" => ast::Operator::GreaterOrEqual,
    "==" => ast::Operator::Equal,
    "!=" => ast::Operator::NotEqual,
};

Comparison: ast::Expression = {
    OrExpr,
    <left: Comparison> <op: ComparisonOperator> <right: OrExpr> =>
        ast::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
};

OrExpr: ast::Expression = {
    XorExpr,
};

XorExpr: ast::Expression = {
    AndExpr,
};

AndExpr: ast::Expression = {
    ShiftExpr,
};

ShiftExpr: ast::Expression = {
    ArithmeticExpr,
};

ArithmeticOperator: ast::Operator = {
    "+" => ast::Operator::Plus,
    "-" => ast::Operator::Minus,
};

ArithmeticExpr: ast::Expression = {
    MultExpr,
    <left: ArithmeticExpr> <op: ArithmeticOperator> <right: MultExpr> =>
        ast::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
};

MultOperator: ast::Operator = {
    "*" => ast::Operator::Multiply,
    "/" => ast::Operator::Divide,
};

MultExpr: ast::Expression = {
    UnaryExpr,
    <left: MultExpr> <op: MultOperator> <right: UnaryExpr> =>
        ast::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
};

UnaryExpr: ast::Expression = {
    PowerExpr,
};

PowerExpr: ast::Expression = {
    Primary,
};

Primary: ast::Expression = {
    Atom,
};

Atom: ast::Expression = {
    <ident: Identifier> => ast::Expression::Identifier { name: ident },
    <literal: Literal> => ast::Expression::Literal { value: literal },
    Enclosure,
};

Identifier: ast::Identifier = {
    <ident: "identifier"> => ast::Identifier::Name { name: ident },
};

Literal: ast::Literal = {
    <i: "integer"> => ast::Literal::Integer { value: i },
};

Enclosure: ast::Expression = {
    "(" <expr: Expression> ")" =>
        ast::Expression::ParenExpression { expr: Box::new(expr) },
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        "indent" => lexer::Tok::Indent,
        "unindent" => lexer::Tok::Unindent,
        "identifier" => lexer::Tok::Identifier { name: <String> },
        "pass" => lexer::Tok::Pass,
        "if" => lexer::Tok::If,
        "while" => lexer::Tok::While,
        "def" => lexer::Tok::Def,
        "or" => lexer::Tok::LogicalOr,
        "and" => lexer::Tok::LogicalAnd,
        "not" => lexer::Tok::LogicalNot,
        "=" => lexer::Tok::Assign,
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Multiply,
        "/" => lexer::Tok::Divide,
        "+=" => lexer::Tok::PlusEquals,
        "-=" => lexer::Tok::MinusEquals,
        "*=" => lexer::Tok::MultiplyEquals,
        "/=" => lexer::Tok::DivideEquals,
        "<" => lexer::Tok::Less,
        ">" => lexer::Tok::Greater,
        "<=" => lexer::Tok::LessOrEqual,
        ">=" => lexer::Tok::GreaterOrEqual,
        "==" => lexer::Tok::Equal,
        "!=" => lexer::Tok::NotEqual,
        "integer" => lexer::Tok::Integer { value: <u32> },
        "(" => lexer::Tok::LPar,
        ")" => lexer::Tok::RPar,
        ":" => lexer::Tok::Colon,
        ";" => lexer::Tok::Semicolon,
        "," => lexer::Tok::Comma,
        "\n" => lexer::Tok::Newline,
    }
}

// vim: ft=rust
