use std::iter::FromIterator;

use lexer;
use ast;

grammar;

pub Program: ast::program::Program = {
    <lines:Line*> => ast::program::Program {
        statements: Vec::from_iter(lines.into_iter().flatten())
    },
};

Line: ast::Suite = {
    Statement,
    "\n" => vec![],
};

Suite: ast::Suite = {
    "\n" "indent" <s: Line+> "unindent" =>
        s.into_iter().flatten().collect(),
    <s: StatementList> "\n" => s,
};

Statement: ast::Suite = {
    StatementList,
    <comp: CompoundStatement> => vec![comp],
};

StatementList: ast::Suite = {
    <s1: SmallStatement> <s2: (";" SmallStatement)*> ";"? "\n" => {
        let mut statements = vec![s1];
        statements.extend(s2.into_iter().map(|e| e.1));
        statements
    }
};

SmallStatement: ast::statement::Statement = {
    AssignStatement,
    AugmentedAssignStatement,
    ExpressionStatement,
    PassStatement,
    ReturnStatement,
};

CompoundStatement: ast::statement::Statement = {
    IfStatement,
    WhileStatement,
    FunctionDeclStatement,
};

AssignStatement: ast::statement::Statement = {
    <ident: Identifier> "=" <expr: Expression> =>
        ast::statement::Statement::Assign { ident: ident, expr: expr, },
};

AugmentedAssignOperator: ast::operator::Operator = {
    "+=" => ast::operator::Operator::PlusEquals,
    "-=" => ast::operator::Operator::MinusEquals,
    "*=" => ast::operator::Operator::MultiplyEquals,
    "/=" => ast::operator::Operator::DivideEquals,
};

AugmentedAssignStatement: ast::statement::Statement = {
    <ident: Identifier> <op: AugmentedAssignOperator> <expr: Expression> =>
        ast::statement::Statement::AugmentedAssign { ident: ident, op: op, expr: expr, },
};

ExpressionStatement: ast::statement::Statement = {
    <expr: Expression> =>
        ast::statement::Statement::Expression { expr: expr, },
};

PassStatement: ast::statement::Statement = {
    "pass" => ast::statement::Statement::Pass,
};

ReturnStatement: ast::statement::Statement = {
    "return" <expr: Expression> =>
        ast::statement::Statement::ReturnStatement { expr: expr },
};

IfStatement: ast::statement::Statement = {
    "if" <expr: Expression> ":" <suite: Suite> <optional: ("else" ":" Suite)?> =>
        ast::statement::Statement::IfStatement {
            expr: expr,
            suite: suite,
            optional: optional.map(|o| o.2),
        },
};

WhileStatement: ast::statement::Statement = {
    "while" <expr: Expression> ":" <suite: Suite> =>
        ast::statement::Statement::WhileStatement {
            expr: expr,
            suite: suite,
        },
};

ArgumentList: Vec<ast::identifier::Identifier> = {
    <l: (<Identifier> ",")*> <e: Identifier> => {
        let mut l = l;
        l.push(e);
        l
    },
};

FunctionDeclStatement: ast::statement::Statement = {
    "def" <ident: Identifier> "(" <args: ArgumentList> ")" ":" <body: Suite> =>
        ast::statement::Statement::FunctionDecl {
            name: ident,
            args: args,
            body: body,
        },
};

Operator: ast::operator::Operator = {
    "+" => ast::operator::Operator::Plus,
    "-" => ast::operator::Operator::Minus,
    "*" => ast::operator::Operator::Multiply,
    "/" => ast::operator::Operator::Divide,
};

Expression: ast::expression::Expression = {
    ConditionalExpression,
};

ConditionalExpression: ast::expression::Expression = {
    OrTest,
};

OrTest: ast::expression::Expression = {
    AndTest,
    <left: OrTest> "or" <right: AndTest> =>
        ast::expression::Expression::BinaryOperation {
            left: Box::new(left),
            op: ast::operator::Operator::LogicalOr,
            right: Box::new(right)
        },
};

AndTest: ast::expression::Expression = {
    NotTest,
    <left: AndTest> "and" <right: NotTest> =>
        ast::expression::Expression::BinaryOperation {
            left: Box::new(left),
            op: ast::operator::Operator::LogicalAnd,
            right: Box::new(right)
        },
};

NotTest: ast::expression::Expression = {
    Comparison,
    "not" <expr: NotTest> =>
        ast::expression::Expression::UnaryOperation {
            op: ast::operator::Operator::LogicalNot,
            expr: Box::new(expr),
        },
};

ComparisonOperator: ast::operator::Operator = {
    "<" => ast::operator::Operator::Less,
    ">" => ast::operator::Operator::Greater,
    "<=" => ast::operator::Operator::LessOrEqual,
    ">=" => ast::operator::Operator::GreaterOrEqual,
    "==" => ast::operator::Operator::Equal,
    "!=" => ast::operator::Operator::NotEqual,
};

Comparison: ast::expression::Expression = {
    OrExpr,
    <left: Comparison> <op: ComparisonOperator> <right: OrExpr> =>
        ast::expression::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
};

OrExpr: ast::expression::Expression = {
    XorExpr,
};

XorExpr: ast::expression::Expression = {
    AndExpr,
};

AndExpr: ast::expression::Expression = {
    ShiftExpr,
};

ShiftExpr: ast::expression::Expression = {
    ArithmeticExpr,
};

ArithmeticOperator: ast::operator::Operator = {
    "+" => ast::operator::Operator::Plus,
    "-" => ast::operator::Operator::Minus,
};

ArithmeticExpr: ast::expression::Expression = {
    MultExpr,
    <left: ArithmeticExpr> <op: ArithmeticOperator> <right: MultExpr> =>
        ast::expression::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
};

MultOperator: ast::operator::Operator = {
    "*" => ast::operator::Operator::Multiply,
    "/" => ast::operator::Operator::Divide,
};

MultExpr: ast::expression::Expression = {
    UnaryExpr,
    <left: MultExpr> <op: MultOperator> <right: UnaryExpr> =>
        ast::expression::Expression::BinaryOperation {
            left: Box::new(left),
            op: op,
            right: Box::new(right),
        },
};

UnaryExpr: ast::expression::Expression = {
    PowerExpr,
};

PowerExpr: ast::expression::Expression = {
    Primary,
};

Primary: ast::expression::Expression = {
    Atom,
    Call,
};

Atom: ast::expression::Expression = {
    <ident: Identifier> => ast::expression::Expression::Identifier { name: ident },
    <literal: Literal> => ast::expression::Expression::Literal { value: literal },
    Enclosure,
};

Call: ast::expression::Expression = {
    <ident: Identifier> "(" <args: CallArgs> ")" =>
        ast::expression::Expression::FunctionCall {
            name: ident,
            args: args,
        },
};

CallArgs: Vec<ast::expression::Expression> = {
    <l: (<Expression> ",")*> <e: Expression> => {
        let mut l = l;
        l.push(e);
        l
    },
};

Identifier: ast::identifier::Identifier = {
    <ident: "identifier"> => ast::identifier::Identifier::Name { name: ident },
};

Literal: ast::literal::Literal = {
    <i: "integer"> => ast::literal::Literal::Integer { value: i },
};

Enclosure: ast::expression::Expression = {
    "(" <expr: Expression> ")" =>
        ast::expression::Expression::ParenExpression { expr: Box::new(expr) },
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        "indent" => lexer::Tok::Indent,
        "unindent" => lexer::Tok::Unindent,
        "identifier" => lexer::Tok::Identifier { name: <String> },
        "pass" => lexer::Tok::Pass,
        "if" => lexer::Tok::If,
        "else" => lexer::Tok::Else,
        "while" => lexer::Tok::While,
        "def" => lexer::Tok::Def,
        "return" => lexer::Tok::Return,
        "or" => lexer::Tok::LogicalOr,
        "and" => lexer::Tok::LogicalAnd,
        "not" => lexer::Tok::LogicalNot,
        "=" => lexer::Tok::Assign,
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Multiply,
        "/" => lexer::Tok::Divide,
        "+=" => lexer::Tok::PlusEquals,
        "-=" => lexer::Tok::MinusEquals,
        "*=" => lexer::Tok::MultiplyEquals,
        "/=" => lexer::Tok::DivideEquals,
        "<" => lexer::Tok::Less,
        ">" => lexer::Tok::Greater,
        "<=" => lexer::Tok::LessOrEqual,
        ">=" => lexer::Tok::GreaterOrEqual,
        "==" => lexer::Tok::Equal,
        "!=" => lexer::Tok::NotEqual,
        "integer" => lexer::Tok::Integer { value: <u32> },
        "(" => lexer::Tok::LPar,
        ")" => lexer::Tok::RPar,
        ":" => lexer::Tok::Colon,
        ";" => lexer::Tok::Semicolon,
        "," => lexer::Tok::Comma,
        "\n" => lexer::Tok::Newline,
    }
}

// vim: ft=rust
